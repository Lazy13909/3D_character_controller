extends CharacterBody3D

@export var camera_mount: Node3D
@export var animation_player: AnimationPlayer
@export var ui: Node3D


const JUMP_VELOCITY = 4.5

var speed : int = 3
var walking_speed : int = 3
var running_speed : int = 5

var running : bool = true
var is_locked : bool = true

var sens_horizontal = .2
var sens_vertical = .2

func _ready() -> void:
	Input.mouse_mode = Input.MOUSE_MODE_CAPTURED
	
func _input(event: InputEvent) -> void:
	if event is InputEventMouseMotion:
		rotate_y(deg_to_rad(-event.relative.x * sens_horizontal))
		ui.rotate_y(deg_to_rad(event.relative.x * sens_horizontal))
		camera_mount.rotate_x(deg_to_rad(-event.relative.y * sens_vertical))
		
func _physics_process(delta: float) -> void:
	if !animation_player.is_playing():
		is_locked = false
	
	if Input.is_action_pressed("kick"):
		animation_player.play("kick")
		is_locked = true
	
	if Input.is_action_pressed("running"):
		speed = running_speed
		running = true
	else:
		speed = walking_speed
		running = false

	if not is_on_floor():
		velocity += get_gravity() * delta

	if Input.is_action_just_pressed("jump") and is_on_floor():
		velocity.y = JUMP_VELOCITY

	var input_dir := Input.get_vector("left", "right", "forward", "backward")
	var direction := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	if direction:
		if !is_locked:
			if running:
				if animation_player.current_animation != "running":
					animation_player.play("running")
			else:
				if animation_player.current_animation != "walking":
					animation_player.play("walking")
			
			ui.look_at(position + direction)
		
		velocity.x = direction.x * speed
		velocity.z = direction.z * speed
	else:
		if !is_locked:
			if animation_player.current_animation != "idle":
				animation_player.play("idle")
	
		velocity.x = move_toward(velocity.x, 0, speed)
		velocity.z = move_toward(velocity.z, 0, speed)
	if !is_locked:
		move_and_slide()
